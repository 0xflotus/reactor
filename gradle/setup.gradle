/*
 * Copyright (c) 2011-2013 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *       http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
apply plugin: "sonar"

sonar.withGlobalProperties {
	props["sonar.host.url"] = "$sonarHostUrl"
	props["sonar.jdbc.url"] = "$sonarJdbcUrl"
	props["sonar.jdbc.driverClassName"] = "$sonarJdbcDriver"
	props["sonar.jdbc.username"] = "$sonarJdbcUsername"
	props["sonar.jdbc.password"] = "$sonarJdbcPassword"
	props["sonar.core.codeCoveragePlugin"] = "jacoco"
	props["sonar.jacoco.reportPath"] = "${buildDir.name}/jacoco.exec"
	props["sonar.links.ci"] = "https://build.springsource.org/browse/REACTOR-CORE"
	props["sonar.links.issue"] = "https://github.com/reactor/reactor/issues"
	props["sonar.links.scm"] = "https://github.com/reactor/reactor.git"
}

task wrapper(type: Wrapper, description: "Create a Gradle self-download wrapper") {
	group = 'Project Setup'
	gradleVersion = "$gradleVersion"
}

subprojects { subproject ->
	apply plugin: 'maven'
	apply plugin: 'java'
	apply plugin: 'groovy'

	ext.optionalDeps = []
	ext.providedDeps = []

	ext.optional = { optionalDeps << it }
	ext.provided = { providedDeps << it }


	install {
		repositories.mavenInstaller {
			customizePom(pom, subproject)
		}
	}
}

def customizePom(def pom, def gradleProject) {
	pom.whenConfigured { generatedPom ->
		// respect 'optional' and 'provided' dependencies
		gradleProject.optionalDeps.each { dep ->
			generatedPom.dependencies.find { it.artifactId == dep.name }?.optional = true
		}
		gradleProject.providedDeps.each { dep ->
			generatedPom.dependencies.find { it.artifactId == dep.name }?.scope = 'provided'
		}
		// eliminate test-scoped dependencies (no need in maven central poms)
		generatedPom.dependencies.removeAll { dep ->
			dep.scope == 'test'
		}

		// add all items necessary for maven central publication
		generatedPom.project {
			name = gradleProject.description
			description = gradleProject.description
			url = 'https://github.com/reactor/reactor'
			organization {
				name = 'reactor'
				url = 'http://github.com/reactor'
			}
			licenses {
				license {
					name 'The Apache Software License, Version 2.0'
					url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
					distribution 'repo'
				}
			}
			scm {
				url = 'https://github.com/reactor/reactor'
				connection = 'scm:git:git://github.com/reactor/reactor'
				developerConnection = 'scm:git:git://github.com/reactor/reactor'
			}
			developers {
				developer {
					id = 'smaldini'
					name = 'Stephane Maldini'
					email = 'smaldini@gopivotal.com'
				}
				developer {
					id = 'jbrisbin'
					name = 'Jon Brisbin'
					email = 'jbrisbin@gopivotal.com'
				}
				developer {
					id = 'awilkinson'
					name = 'Andy Wilkinson'
					email = 'awilkinson@gopivotal.com'
				}
			}
		}
	}
}